     FFRS000DFCF  E                    WORKSTN      KINFDS WSDS
     FAGENTSL IF  F     136 66AI     5 DISK
     I*
     IAGENTSL NS  01
     I                                    B   1   40AGNMBR
     I                                        5  70 AGNAME
     I                                       71 136 AGPASS
     I*
     I              X'31'                 C         @F01
     I              X'32'                 C         @F02
     I              X'33'                 C         @F03
     I              X'34'                 C         @F04
     I              X'39'                 C         @F09
     I              X'3A'                 C         @F10
     I              X'3C'                 C         @F12
     I              X'F1'                 C         @ENTER
     I              X'F3'                 C         @HELP
     I              X'F6'                 C         @PRINT
     I*
     I              X'7D'                 C         APOST
     I*
     I              '0'                   C         @FALSE
     I              '1'                   C         @TRUE
     I*
     I           SDS
     I                                        1  10 @PGM                   _ERR_
     I                                       11  150@STAT                  _ERR_
     I                                       37  390@PARMS                 _ERR_
     I                                       40  46 @MSGID                 _ERR_
     I                                       91 169 @DATA                  _ERR_
     I                                      244 253 @JOBNA                 _ERR_
     I                                      254 263 @USRNA                 _ERR_
     I                                      264 269 @JOBNO                 _ERR_
     I                                      304 313 @SRCF                  _ERR_
     I                                      314 323 @SRCL                  _ERR_
     I                                      324 333 @SRCM                  _ERR_
     I*
     IAPIERR      DS
     I I            256                   B   1   40ERRSIZ
     I I            0                     B   5   80ERRLEN
     I I                                      9  15 ERRMIC
     I I                                     16  16 ERRNBR
     I I                                     17 256 ERRDTA
     I*
     I            DS
     I I            0                     B   1   40MSGCTR
     I I            80                    B   5   80MSGLEN
     I I            'FRSMSGF   FLGHT400  '    9  28 MSGF
     I I            '*INFO     '             29  38 MSGTYP
     I I            '    '                   39  42 MSGKEY
     I*
     I            DS
     I I            0                     B   1   40RMVCTR
     I I            '*         '              5  14 RMVMSQ
     I I            '*ALL      '             15  24 RMVCLR
     I I            '    '                   25  28 RMVKEY
     I*
     IWSDS        DS
     I                                      369 369 KEY
     I*
     I            DS
     I                                        1  66 AGNKEY
     I                                    B   1   20AGNLEN
     I                                        3  66 AGNNAM
     I*
     I            DS
     I                                        1  66 PWDKEY
     I                                    B   1   20PWDLEN
     I                                        3  66 PWDNAM
     I*
     ICRTCMD      DS
     I I            'GO FLGHT400/FRSMANT'     1  19 GOCMD1
     I*
     ICRTCM2      DS
     I I            'GO FLGHT400/FRSREPT'     1  19 GOCMD2
     I*
     C*
     C           *ENTRY    PLIST
     C                     PARM           RUNTYP  6
     C*                                                     * D=DELETE
     C                     RESETLOGONR                      * REFRESH SCREEN -
     C                     EXSR INZSR
     C*
     C           @EXIT     DOUEQ@TRUE
     C*
     C                     WRITEMSGCTL                      * WRITE MSGCTL
     C                     EXFMTLOGONR                      * DISPLAY SCREEN
     C*
     C           KEY       CASEQ@F02      @F02SR            * F02 REFRESH
     C           KEY       CASEQ@F03      @F03SR            * F03 EXIT
     C           KEY       CASEQ@F04      @F04SR            * F03 EXIT
     C           KEY       CASEQ@F09      @F09SR            * F03 EXIT
     C           KEY       CASEQ@F12      @F12SR            * F12 CANCEL
     C                     CAS            @ERRCK            * CK FOR ERRORS
     C                     ENDCS
     C*
     C           KEY       IFEQ @F10                        * F10 PRESSED
     C           *INLR     ANDNE*ON                         * F10 PRESSED
     C           *IN50     ANDEQ*OFF                        * 50 OFF=NO ERRS
     C                     EXSR @F10SR                      * CREATE RECRD
     C                     LEAVE                            * LEAVE DO LOOP
     C                     ENDIF                            * END IF
     C*
     C                     ENDDO                            * END DO
     C*
     C           END       TAG
     C*
     C                     MOVE *ON       *INLR             * LAST RECORD=ON
     C*
     C*
     C*
     C           INZSR     BEGSR                            * SET OUTPT SCREEN 1
     C*
     C                     SETOF                     505152 * ERROR IND. SC 1
     C*
     C                     MOVE ' '       MSGID   7
     C                     MOVE *BLANKS   MSGDTA 80
     C                     MOVE @PGM      MSGQUE 10
     C*
     C                     MOVE @FALSE    @EXIT   1
     C                     MOVE @PGM      PGMQ
     C*
     C           ENDINZ    ENDSR
     C*
     C*
     C*
     C           @ERRCK    BEGSR                            * CHK INPUT DATA
     C*
     C                     SETOF                       44   * ERROR IND. SC 1
     C                     SETOF                     505152 * ERROR IND. SC 1
     C*
     C                     EXSR @RMVMS
     C*
     C           AGNTNM    IFEQ *BLANKS                     * RUN INTERACTIVE
     C                     SETON                       5051 * SET ERR INDICATOR
     C                     MOVE 'FRS0050' MSGID             * SET ERR MESSAGE
     C                     EXSR @SNDMS                      * SND ERR MESSAGE
     C                     END
     C*
     C           PASSWD    IFEQ *BLANKS                     * RUN INTERACTIVE
     C                     SETON                       5052 * SET ERR INDICATOR
     C                     MOVE 'FRS0055' MSGID             * SET ERR MESSAGE
     C                     EXSR @SNDMS                      * SND ERR MESSAGE
     C                     GOTO ERREND
     C                     END
     C*
     C                     MOVE *BLANKS   AGNKEY            * SET ERR MESSAGE
     C           ' '       CHEKRAGNTNM    AGNLEN            * SET ERR MESSAGE
     C                     MOVELAGNTNM    AGNNAM            * SET ERR MESSAGE
     C*
     C           AGNKEY    CHAINAGENTSL              44     * RUN INTERACTIVE
     C           *IN44     IFEQ @TRUE                       * RUN INTERACTIVE
     C                     SETON                       5051 * SET ERR INDICATOR
     C                     MOVE 'FRS0052' MSGID             * SET ERR MESSAGE
     C                     EXSR @SNDMS                      * SND ERR MESSAGE
     C                     GOTO ERREND
     C                     END
     C*
     C                     MOVE *BLANKS   PWDKEY            * SET ERR MESSAGE
     C           ' '       CHEKRPASSWD    PWDLEN            * SET ERR MESSAGE
     C                     MOVELPASSWD    PWDNAM            * SET ERR MESSAGE
     C*
     C           PWDKEY    IFNE AGPASS                      * RUN INTERACTIVE
     C                     SETON                       5052 * SET ERR INDICATOR
     C                     MOVE 'FRS0057' MSGID             * SET ERR MESSAGE
     C                     EXSR @SNDMS                      * SND ERR MESSAGE
     C                     GOTO ERREND
     C                     END
     C*
     C           ERREND    ENDSR
     C*
     C*
     C           @RMVMS    BEGSR
     C                     CALL 'QMHRMVPM'                  * PGM TO RMV PGM MSG
     C                     PARM           RMVMSQ            * MESSAGE DATA
     C                     PARM           RMVCTR            * MESSAGE DATA
     C                     PARM           RMVKEY            * MESSAGE DATA
     C                     PARM           RMVCLR
     C                     PARM           APIERR
     C                     ENDSR
     C*
     C*
     C           @SNDMS    BEGSR
     C                     CALL 'QMHSNDPM'                  * PGM TO SND PGM MSG
     C                     PARM           MSGID             * MESSAGE ID
     C                     PARM           MSGF              * MESSAGE FILE
     C                     PARM           MSGDTA            * MESSAGE DATA
     C                     PARM           MSGLEN            * MESSAGE DATA
     C                     PARM           MSGTYP            * MESSAGE DATA
     C                     PARM           MSGQUE            * MESSAGE DATA
     C                     PARM           MSGCTR            * MESSAGE DATA
     C                     PARM           MSGKEY            * MESSAGE DATA
     C                     PARM           APIERR
     C                     ENDSR
     C*
     C*
     C           @F02SR    BEGSR                            * F2 SUBROUTINE
     C                     SETOF                     505152 * ERROR IND. SC 1
     C                     RESETLOGONR                      * REFRESH SCREEN -
     C                     EXSR @RMVMS
     C                     ENDSR
     C*
     C*
     C           @F03SR    BEGSR                            * F3 SUBROUTINE
     C                     MOVE @TRUE     @EXIT             * SET EXIT FLAG ON
     C                     ENDSR
     C*
     C*
     C           @F04SR    BEGSR                            * F3 SUBROUTINE
     C                     MOVE *BLANKS   AGNTNM            * RESET STATUS IND
     C                     CALL 'FRS401'                    * SELCT START INTRVL
     C                     PARM           AGNTNM            * RETURN START TIME
     C                     ENDSR
     C*
     C*
     C           @F09SR    BEGSR                            * F9 SUBROUTINE
     C                     CALL 'QUSCMDLN'                  * GET CMD LINE
     C                     ENDSR
     C*
     C*
     C           @F10SR    BEGSR                            * F10 SUBROUTINE
     C*
     C                     MOVELAGNMBR    AGENT#  90
     C*
     C                     SELEC                            * F02 REFRESH
     C           RUNTYP    WHEQ 'CREATE'                    * F03 EXIT
     C                     CALL 'FRS001'                    * GET CMD LINE
     C                     PARM           AGENT#
     C           RUNTYP    WHEQ 'UPDATE'                    * F03 EXIT
     C                     CALL 'FRS009'                    * GET CMD LINE
     C                     PARM           AGENT#
     C                     PARM           RUNTYP
     C           RUNTYP    WHEQ 'DSPLAY'                    * F03 EXIT
     C                     CALL 'FRS009'                    * GET CMD LINE
     C                     PARM           AGENT#
     C                     PARM           RUNTYP
     C           RUNTYP    WHEQ 'DELETE'                    * F03 EXIT
     C                     CALL 'FRS009'                    * GET CMD LINE
     C                     PARM           AGENT#
     C                     PARM           RUNTYP
     C           RUNTYP    WHEQ 'FAX   '                    * F03 EXIT
     C                     CALL 'FRS009'                    * GET CMD LINE
     C                     PARM           AGENT#
     C                     PARM           RUNTYP
     C           RUNTYP    WHEQ 'MAINT '                    * F03 EXIT
     C                     Z-ADD19        CMDLEN 155
     C                     CALL 'QCMDEXC'
     C                     PARM           CRTCMD            * G=GET
     C                     PARM           CMDLEN            * G=GET
     C           RUNTYP    WHEQ 'REPORT'                    * F03 EXIT
     C                     Z-ADD19        CMDLEN
     C                     CALL 'QCMDEXC'
     C                     PARM           CRTCM2            * G=GET
     C                     PARM           CMDLEN            * G=GET
     C                     ENDSL                            * F12 CANCEL
     C*
     C           F10END    ENDSR
     C*
     C*
     C           @F12SR    BEGSR
     C                     MOVE @TRUE     @EXIT
     C                     ENDSR
     C*
